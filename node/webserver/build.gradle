apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'net.corda.plugins.quasar-utils'
apply plugin: 'net.corda.plugins.publish-utils'

description 'Corda Webserver'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
    jcenter()
    maven {
        url 'https://dl.bintray.com/kotlin/exposed'
    }
}

dependencies {
    compile project(':core')
    compile project(':node')

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Web stuff: for HTTP[S] servlets
    compile "org.eclipse.jetty:jetty-servlet:${jetty_version}"
    compile "org.eclipse.jetty:jetty-webapp:${jetty_version}"
    compile "javax.servlet:javax.servlet-api:3.1.0"
    compile "org.jolokia:jolokia-agent-war:2.0.0-M1"
    compile "commons-fileupload:commons-fileupload:1.3.2"

    // Log4J: logging framework (with SLF4J bindings)
    compile "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_version}"
    compile "org.apache.logging.log4j:log4j-core:${log4j_version}"
    compile "org.apache.logging.log4j:log4j-web:${log4j_version}"

    // Jersey for JAX-RS implementation for use in Jetty
    compile "org.glassfish.jersey.core:jersey-server:${jersey_version}"
    compile "org.glassfish.jersey.containers:jersey-container-servlet-core:${jersey_version}"
    compile "org.glassfish.jersey.containers:jersey-container-jetty-http:${jersey_version}"
    // NOTE there is a Jackson version clash between jersey-media-json-jackson (v2.5.4) and jackson-module-kotlin (v.2.5.5)
    // Have not found an Issue in the issue tracker for Jersey for this issue
    compile ("org.glassfish.jersey.media:jersey-media-json-jackson:${jersey_version}") {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
    }
    compile ("com.fasterxml.jackson.module:jackson-module-kotlin:${jackson_version}") {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
    }
    compile "com.fasterxml.jackson.core:jackson-annotations:${jackson_version}"
}

task run(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.node.webserver.MainKt'
}